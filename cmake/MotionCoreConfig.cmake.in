@PACKAGE_INIT@

include(CMakeFindDependencyMacro)
find_dependency(Threads)
find_dependency(nlohmann_json CONFIG)

# Prefer a compatible fastcdr package config (commonly v1 from system packages).
# Some machines also have a newer fastcdr installed under /usr/local which may
# be ABI-incompatible with the fastrtps binary.
set(_wxz_system_fastcdr_dir "/usr/lib/x86_64-linux-gnu/cmake/fastcdr")
if(NOT DEFINED fastcdr_DIR)
	if(EXISTS "${_wxz_system_fastcdr_dir}/fastcdr-config.cmake" OR EXISTS "${_wxz_system_fastcdr_dir}/fastcdrConfig.cmake")
		set(fastcdr_DIR "${_wxz_system_fastcdr_dir}")
	endif()
elseif(fastcdr_DIR MATCHES "^/usr/local" AND EXISTS "${_wxz_system_fastcdr_dir}/fastcdr-config.cmake")
	set(fastcdr_DIR "${_wxz_system_fastcdr_dir}")
endif()

# Resolve fastcdr before fastrtps to avoid target collisions that can lock in an
# ABI-incompatible fastcdr.
find_dependency(fastcdr CONFIG)
find_dependency(fastrtps CONFIG)

include("${CMAKE_CURRENT_LIST_DIR}/MotionCoreTargets.cmake")

set(MotionCore_INCLUDE_DIR "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_INCLUDEDIR@/MotionCore")
set(MotionCore_CONFIG_DIR "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_DATAROOTDIR@/MotionCore/config")
set(MotionCore_RESOURCE_DIR "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_DATAROOTDIR@/MotionCore/resources")
set(MotionCore_DOC_DIR "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_DATAROOTDIR@/MotionCore/docs")
